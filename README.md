
```bsl
&НаКлиенте
Процедура ЗагрузитьИзExcel(Команда)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.ПолноеИмяФайла		= "";
	ДиалогОткрытияФайла.МножественныйВыбор	= Ложь;
	ДиалогОткрытияФайла.Заголовок			= "Выберите файл Excel";
	ДиалогОткрытияФайла.Фильтр				= "Книга Excel(*.xlsx)|*.xlsx|Книга Excel(*.xls)|*.xls";
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
		Если МассивФайлов.Количество() <> 0 тогда
			ВыбранныйФайл = Новый Файл(МассивФайлов[0]);
			ReadExcel(ВыбранныйФайл.ПолноеИмя, 1);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ReadExcel(ИмяФайла, Лист = 1)
	
	Попытка    
		Excel = Новый COMОбъект("Excel.Application");
	Исключение
		Предупреждение("Excel на компьютере не установлен!");
		Возврат;
	КонецПопытки;	
	
	Excel.Workbooks.Open(СокрЛП(ИмяФайла));
	
	КоличествоСтрок		= Excel.Sheets(Лист).Cells.SpecialCells(11).Row;    // строка последней ячейки
	КоличествоСтолбцов	= Excel.Sheets(Лист).Cells.SpecialCells(11).Column; // столбец последней ячейки
	
	СоздатьТаблицу(КоличествоСтолбцов);
	ТЗ = ЭтаФорма["ТаблицаДанных"];
	
	Для А = 1 По КоличествоСтрок Цикл
		НовСтрока = ТЗ.Добавить();
		Для Б = 1 по КоличествоСтолбцов Цикл
			НовСтрока["Колонка" + (Б)] = СокрЛП(Excel.Sheets(Лист).Cells(ТЗ.Индекс(НовСтрока)+1,Б).Value);
		КонецЦикла;
	КонецЦикла;
	
	Excel.Workbooks.Close();  
	
КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицу(КоличествоКолонок)
	
	МассивУдаляемыхРеквизитов = Новый Массив;
	
	// удаление элемента и реквизита таблицы с прошлой загрузки
	Если КоличествоКолонокПрошлыйРаз > 0 Тогда
		Элементы.Удалить(Элементы["ТаблицаДанных"]);
					
		МассивУдаляемыхРеквизитов.Добавить("ТаблицаДанных");
	КонецЕсли;
	
	// создание реквизита "ТаблицаДанных"
	МассивРеквизитов = Новый Массив;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ТаблицаЗначений"));
	ОписаниеТипа = Новый ОписаниеТипов(МассивТипов);

	МассивРеквизитов.Добавить(Новый РеквизитФормы("ТаблицаДанных", ОписаниеТипа));
	
	// создание реквизитов таблицы значений 	            
	
	МассивКC = Новый Массив;	
	МассивКC.Добавить(Тип("Строка"));	
	ОписаниеТиповСтрока = Новый ОписаниеТипов(МассивКC, , ,Новый КвалификаторыСтроки(0));
	
	ТЗ = Новый ТаблицаЗначений;
	Для Сч = 1 По КоличествоКолонок Цикл
		ТЗ.Колонки.Добавить("Колонка" + Формат(Сч, "ЧГ="), ОписаниеТиповСтрока);
	КонецЦикла;	
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "ТаблицаДанных"));	
	КонецЦикла;	
	
	// обновляем реквизиты
	ИзменитьРеквизиты(МассивРеквизитов, МассивУдаляемыхРеквизитов);      
	
	// добавляем элементы на форму	
	ЭлементТаблицы = Элементы.Добавить("ТаблицаДанных", Тип("ТаблицаФормы"));
	ЭлементТаблицы.ПутьКДанным = "ТаблицаДанных";
	ЭлементТаблицы.Отображение = ОтображениеТаблицы.Список;
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		
		ЭлементКолонки = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ЭлементТаблицы);       
		ЭлементКолонки.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементКолонки.ПутьКДанным = "ТаблицаДанных." + Колонка.Имя;
		ЭлементКолонки.Ширина = 10;
		
	КонецЦикла;
	
	КоличествоКолонокПрошлыйРаз = КоличествоКолонок;
	
КонецПроцедуры
```
